#include EAstdlib.event
#include ParseDefinitions.event

// Thanks to Colorz and Contro for figuring out this trick!
#define SetLabel(parentLabel) "LABEL (currentOffset - (parentLabel))"
#define LabelDest(parentLabel, destLabel) "((destLabel) - (parentLabel))"

#define ScrollText(id) "SVAL sB (-1); TUTORIALTEXTBOXSTART; TEXTSHOW (id); TEXTEND;"
#define CursorFlash(c, time) "CURSOR_FLASHING c; STAL (time); REMOVE_CURSORS;"
#define CursorFlash(c) "FlashCursor(c, 30)"

PUSH
// prologue events
EventPointerTable(0x7,ChEvents)
// prologue map
EventPointerTable(0x4,ChMap)

// (initial) goal window
ORG 0x8B091A
SHORT 0x01A2
POP

#define CastleCoords [5,16]

#define EarlyWarningId 10
#define MageHouseId 11

#define StartBadGuy 0x8E
#define BadGuy 0x80
#define TheSniper 0x8F

ALIGN 4
ChMap:
#incbin map.dmp

ALIGN 4
ChEvents:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN AltMiscEventsA AltMiscEventsB AltMiscEventsC
POIN Tutorial
POIN TrapData TrapData
POIN PlayerUnits PlayerUnits
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0,LoadFlier,2)
TurnEventPlayer(0,SpawnEnemies,4)
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
House(MageHouseId, MageHouse, 15, 12)
//Seize(0x3, DontSeizeBaka, CastleCoords)
Seize(0x3, DontSeizeBaka, 5, 16)
END_MAIN

MiscBasedEvents:
AREA EarlyWarningId CheckEarlyWarning [5,16] [5,17]
CauseGameOverIfLordDies
END_MAIN

TrapData:
END_MAIN

#define PlayerUnit(char, cls, lvl) "UNIT char cls Eirika Level(lvl,Ally,0)"

PlayerUnits:
PlayerUnit(Eirika,EirikaMasterLord,1) [9,0] 0 0 1 EirMoves [Rapier,Vulnerary] NoAI
PlayerUnit(Seth,Paladin,10) [10,0] 0 0 1 SethMoves [SilverLance,SteelSword] NoAI
UNIT

BaseEnemies:
UNIT StartBadGuy Brigand 0 Level(18,Enemy,1) [13,3] 0 0 1 BanditMoves1 [IronAxe,Vulnerary] NoAI
UNIT StartBadGuy Brigand 0 Level(15,Enemy,1) [13,3] 0 0 1 BanditMoves2 [IronAxe,Vulnerary] NoAI
UNIT

Flier:
PlayerUnit(Vanessa,FalcoKnight,4) [0,7] 0 0 1 FlierMoves [SlimLance,HeavySpear] NoAI
UNIT TheSniper Sniper 0 Level(5,Enemy,1) [0,11] 0 0 1 ArcherMoves [Longbow] AttackInRangeAI
UNIT

Turn2BadGuys:
UNIT BadGuy Warrior 0 Level(3,Enemy,1) [17,3] 0 0 0 0 [SilverAxe,IronBow] NoAI
UNIT BadGuy Fighter 0 Level(16,Enemy,1) [17,3] 0 0 1 m1 [IronAxe] NoAI
UNIT BadGuy Fighter 0 Level(18,Enemy,1) [17,3] 0 0 1 m2 [SteelAxe] NoAI
UNIT

Turn3BadGuys:
UNIT BadGuy Paladin 0 Level(4,Enemy,1) [19,15] 0 0 1 m3 [SilverLance,SteelSword] NoAI
UNIT BadGuy Cavalier 0 Level(16,Enemy,1) [19,16] 0 0 1 m4 [IronLance] NoAI
UNIT BadGuy Cavalier 0 Level(18,Enemy,1) [19,15] 0 0 1 m5 [SteelSword] NoAI
UNIT

BoltingMage:
PlayerUnit(LArachel,MageKnight_F,1) [15,12] 0 0 1 MageMoves [Elfire,Bolting,Vulnerary] NoAI
UNIT

NonBoltingMage:
PlayerUnit(LArachel,MageKnight_F,1) [15,12] 0 0 1 MageMoves [Elfire,Physic,Vulnerary] NoAI
UNIT

EirMoves:
//REDA [8,2]
REDA CastleCoords
SethMoves:
REDA [6,5]
FlierMoves:
REDA [4,4]
MageMoves:
REDA [14,12]
ArcherMoves:
REDA [2,11]
BanditMoves1:
REDA [11,5]
BanditMoves2:
REDA [10,6]
m1:
REDA [16,2]
m2:
REDA [17,4]
m3:
REDA [17,14]
m4:
REDA [18,13]
m5:
REDA [18,15]

ALIGN 4

BeginningScene:
LOAD1 1 PlayerUnits
ENUN
CursorFlash(Eirika)
Text(Woodland, OpeningText)
LOAD1 1 BaseEnemies
ENUN
// We need to do this due to a bug in ColorzCore macro parsing that doesn't respect
// [bracketed,coordinates] as a single parameter.
#define hlcoords [11,5]
CursorFlash(hlcoords)
#undef hlcoords
Text(Woodland, OpeningBandits)
ScrollText(ExplainHidden)
ScrollText(ExplainGrade)
ScrollText(ExplainMutuallyExclusive)
CAMERA CastleCoords
CursorFlash(CastleCoords)
STAL 30
CursorFlash(CastleCoords)
ScrollText(ExplainGoToCastle)
ScrollText(ExplainSeize)
NoFade
ENDA

LoadFlier:
LOAD1 1 Flier
ENUN
CAMERA Vanessa
CursorFlash(Vanessa)
Text(0x0905)
EVBIT_T 0x9
LOAD1 1 Turn2BadGuys
ENUN
EVBIT_F 0x9
NoFade
ENDA

MageHouse: {
  CHECK_TURNS
  SVAL s1 5
  BLT LabelDest(MageHouse,TooLate) s1 sC
  CHECK_ACTIVEID Eirika
  BEQ LabelDest(MageHouse,NotEirikaEarly) sC s0
  Text(HouseBG, EarlyMageEirika)
  GOTO LabelDest(MageHouse,LoadBoltingMage)

  NotEirikaEarly:
  SetLabel(MageHouse)
  Text(HouseBG, EarlyMageGeneric)

  LoadBoltingMage:
  SetLabel(MageHouse)
  LOAD1 1 BoltingMage
  ENUN
  GOTO LabelDest(MageHouse,Done)

  TooLate:
  SetLabel(MageHouse)
  CHECK_ACTIVEID Eirika
  BEQ LabelDest(MageHouse,NotEirikaLate) sC s0
  Text(HouseBG, LateMageEirika)
  GOTO LabelDest(MageHouse,NonBoltingMage)

  NotEirikaLate:
  SetLabel(MageHouse)
  Text(HouseBG, LateMageGeneric)

  LoadNonBoltingMage:
  SetLabel(MageHouse)
  LOAD1 1 NonBoltingMage
  ENUN
  CursorFlash(LArachel)
  Text(SorryLostBolting)

  Done:
  SetLabel(MageHouse)
  NoFade
  ENDA
}

CheckEarlyWarning: {
  CHECK_INAREA Eirika [5,16] [1,1]
  BEQ LabelDest(CheckEarlyWarning,NotThere) sC s0
  CALL SecretEnding
  GOTO Done

  NotThere:
  SetLabel(CheckEarlyWarning)
  CHECK_TURNS
  SVAL s1 4
  BLT LabelDest(CheckEarlyWarning,Done) s1 sC
  ENUF EarlyWarningId

  Done:
  SetLabel(CheckEarlyWarning)
  NoFade
  ENDA
}

SpawnEnemies: {
  CHECK_INAREA Eirika CastleCoords [1,1]
  BEQ LabelDest(SpawnEnemies,Failed) sC s0
  CALL SecretEnding
  GOTO LabelDest(SpawnEnemies,Done)

  Failed:
  SetLabel(SpawnEnemies)
  POPUP 0x00D5 0

  Done:
  SetLabel(Done)
  NoFade
  ENDA
}

SecretEnding: {
  CALL EndingScene
  NoFade
  ENDA
}

EndingScene: {
  NoFade
  ENDA
}

DontSeizeBaka: {
  Text(DontSeize1)
  ScrollText(DontSeize2)
  ScrollText(DontSeize3)
  ScrollText(DontSeize4)
  ScrollText(DontSeize5)
  GameOver
  NoFade
  ENDA
}

ALIGN 4
