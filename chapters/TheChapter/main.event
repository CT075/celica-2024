#include EAstdlib.event
#include ParseDefinitions.event

// Thanks to Colorz and Contro for figuring out this trick!
#define SetLabel(parentLabel) "LABEL (currentOffset - (parentLabel))"
#define LabelDest(parentLabel, destLabel) "((destLabel) - (parentLabel))"

#define ScrollText(id) "SVAL sB (-1); TUTORIALTEXTBOXSTART; TEXTSHOW (id); TEXTEND;"
#define CursorFlash(c, time) "CURSOR_FLASHING c; STAL (time); REMOVE_CURSORS;"
#define CursorFlash(c) "FlashCursor(c, 30)"

PUSH
// prologue events
EventPointerTable(0x7,ChEvents)
// prologue map
EventPointerTable(0x4,ChMap)

// (initial) goal window
ORG 0x8B091A
SHORT 0x01A2

ORG 0x8B0916
BYTE -1

PUSH
  ORG 0x5A8A24
  SHORT Gilliam 0
POP
POP

#define CastleCoords [5,16]
#define MageHouseCoords [15,12]
#define SouthFort [10,18]

#define EarlyWarningId 10
#define MageHouseId 11
#define MemberCardId 12
#define GilliamTalkId 13
#define LateVisitId 14
#define MatthewTalkId 15
#define MatthewSecretId 16

#define VisitedSecretShopId 131
#define MatthewFoughtEphidelId 132
#define RoutedId 6

#define StartBadGuy 0x8E
#define BadGuy 0x80
#define TheSniper 0x8F
#define Ephidel Lyon_Ch17
#define Matthew Colm

#include REDAHelpers.event

ALIGN 4
ChMap:
#incbin map.dmp

ALIGN 4
ChEvents:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN AltMiscEventsA AltMiscEventsB AltMiscEventsC
POIN Tutorial
POIN TrapData TrapData
POIN PlayerUnits PlayerUnits
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0,LoadFlier,2)
TurnEventPlayer(0,SpawnEnemies,4)
TurnEventPlayer(0,SpawnReinforcements1,5)
TurnEventPlayer(0,SpawnReinforcements2,6)
TurnEventPlayer(0,EphidelAggro,8)
DefeatBoss(DidNotVisit)
END_MAIN

CharacterBasedEvents:
CharacterEvent(GilliamTalkId, TalkToGilliam, Eirika, Gilliam)
CharacterEvent(MatthewTalkId, MatthewEirikaChat, Eirika, Matthew)
CharacterEvent(MatthewTalkId, MatthewSethChat, Seth, Matthew)
END_MAIN

LocationBasedEvents:
//House(MageHouseId, MageHouse, MageHouseCoords)
House(MageHouseId, MageHouse, 15, 12)
//Seize(0x3, DontSeizeBaka, CastleCoords)
Seize(3, DontSeizeBaka, 5, 16)
SecretShop(SecretShopContents,0,7)
END_MAIN

MiscBasedEvents:
AREA EarlyWarningId CheckEarlyWarning [5,16] [5,17]
AREA MemberCardId GiveMemberCard [0,19] [0,19]
AREA 3 LateVisit [5,16] [5,16]
CauseGameOverIfLordDies
AFEV MatthewSecretId MatthewSecretShopChat MatthewTalkId
END_MAIN

TrapData:
END_MAIN

#define PlayerUnit(char, cls, lvl) "UNIT char cls Eirika Level(lvl,Ally,0)"

PlayerUnits:
PlayerUnit(Eirika,EirikaMasterLord,1) [9,0] 0 0 1 C_8_2 [Rapier,Elixir] NoAI
PlayerUnit(Seth,Paladin,10) [10,0] 0 0 1 C_6_5 [SilverLance,SteelSword] NoAI
UNIT

BaseEnemies:
UNIT StartBadGuy Brigand 0 Level(18,Enemy,1) [13,3] 0 0 1 C_11_5 [IronAxe,Vulnerary] NoAI
UNIT StartBadGuy Brigand 0 Level(15,Enemy,1) [13,3] 0 0 1 C_10_6 [IronAxe,Vulnerary] NoAI
UNIT

Flier:
PlayerUnit(Vanessa,FalcoKnight,4) [0,7] 0 0 1 C_4_4 [SteelLance,SteelSword,Vulnerary] NoAI
UNIT TheSniper Sniper 0 Level(5,Enemy,0) [0,11] 0 0 1 C_2_11 [Longbow] AttackInRangeAI
UNIT

Turn2BadGuys:
UNIT BadGuy Warrior 0 Level(3,Enemy,1) [17,3] 0 0 0 0 [SilverAxe] NoAI
UNIT BadGuy Fighter 0 Level(16,Enemy,1) [17,3] 0 0 1 C_16_2 [IronAxe] NoAI
UNIT BadGuy Fighter 0 Level(18,Enemy,1) [17,3] 0 0 1 C_17_4 [SteelAxe] NoAI
UNIT

BoltingMage:
PlayerUnit(LArachel,MageKnight_F,1) [15,12] 0 0 1 C_14_12 [Elfire,Bolting,Vulnerary] NoAI
UNIT

NonBoltingMage:
PlayerUnit(LArachel,MageKnight_F,1) [15,12] 0 0 1 C_14_12 [Elfire,Physic,Vulnerary] NoAI
UNIT

GateGuard:
UNIT Gilliam General Gilliam Level(10,NPC,0) CastleCoords 0 0 0 0 [SilverLance,Elixir] NeverMoveAI
UNIT

RightCavs:
UNIT BadGuy GreatKnight Ephidel Level(4,Enemy,1) [19,15] 0 0 1 C_17_14 [SilverAxe,SteelLance] ChaseOnceApproachedAI
UNIT BadGuy Cavalier Ephidel Level(16,Enemy,1) [19,16] 0 0 1 C_18_13 [IronLance] ChaseOnceApproachedAI
UNIT BadGuy Cavalier Ephidel Level(18,Enemy,1) [19,15] 0 0 1 C_18_15 [SteelSword] ChaseOnceApproachedAI
UNIT

MiddleInfantry:
UNIT BadGuy Berserker Ephidel Level(3,Enemy,1) [14,11] 0 0 1 C_15_9 [KillerAxe] AttackInRangeAI
UNIT BadGuy Brigand Ephidel Level(18,Enemy,1) [14,11] 0 0 1 C_13_9 [IronAxe] AttackInRangeAI
UNIT BadGuy Brigand Ephidel Level(16,Enemy,1) [14,11] 0 0 1 C_14_8 [HandAxe] AttackInRangeAI
UNIT BadGuy Brigand Ephidel Level(19,Enemy,1) [14,11] 0 0 1 C_16_10 [SteelAxe] AttackInRangeAI
UNIT

TopLeftWyverns:
UNIT BadGuy WyvernKnight Ephidel Level(4,Enemy,1) [0,3] 0 0 1 C_4_5 [SilverLance] NoAI
UNIT BadGuy WyvernKnight Ephidel Level(3,Enemy,1) [0,3] 0 0 1 C_4_7 [SilverLance] NoAI
UNIT BadGuy WyvernRider Ephidel Level(19,Enemy,1) [0,3] 0 0 1 C_3_4 [SteelLance] NoAI
UNIT BadGuy WyvernRider Ephidel Level(17,Enemy,1) [0,4] 0 0 1 C_3_6 [Javelin] NoAI
UNIT

TopLeftSwords:
UNIT BadGuy Swordmaster Ephidel Level(5,Enemy,1) [5,0] 0 0 1 C_7_5 [BraveSword] NoAI
UNIT BadGuy Myrmidon Ephidel Level(16,Enemy,1) [5,0] 0 0 1 C_7_4 [KillingEdge] NoAI
UNIT BadGuy Myrmidon Ephidel Level(18,Enemy,1) [5,0] 0 0 1 C_6_5 [SteelSword] NoAI
UNIT

EphidelGroup:
UNIT Ephidel Necromancer 0 Level(20,Enemy,0) [19,0] 0 0 1 C_17_3 [Naglfar] NeverMoveAI
UNIT BadGuy Sage Ephidel Level(9,Enemy,1) [19,0] 0 0 1 C_18_4 [Fimbulvetr] MoveWithLeaderAI
UNIT BadGuy Sage Ephidel Level(7,Enemy,1) [19,1] 0 0 1 C_16_2 [Elfire] MoveWithLeaderAI
UNIT

EphraimReinforcements:
PlayerUnit(Ephraim,EphraimMasterLord,5) SouthFort 0 0 1 C_13_12 [Reginleif,Elixir] NoAI
PlayerUnit(Duessel,GreatKnight,10) SouthFort 0 0 1 C_12_13 [SilverAxe,Spear] NoAI
PlayerUnit(Innes,Sniper,4) SouthFort 0 0 1 C_10_14 [SilverBow,Elixir] NoAI
PlayerUnit(Gerik,Hero,8) SouthFort 0 0 1 C_11_13 [SteelBlade,Tomahawk] NoAI
PlayerUnit(Amelia,General,1) SouthFort 0 0 1 C_14_14 [SteelAxe,Javelin,SteelLance] NoAI
PlayerUnit(Matthew,Rogue,6) SouthFort 0 0 1 C_12_15 [KillingEdge,Vulnerary] NoAI
PlayerUnit(Ross,Berserker,1) SouthFort 0 0 1 C_12_12 [KillerAxe] NoAI
PlayerUnit(Cormag,WyvernLord,7) SouthFort 0 0 1 C_15_15 [KillerLance,Vulnerary] NoAI
PlayerUnit(Moulder,Sage,7) SouthFort 0 0 1 C_12_16 [Fimbulvetr,Physic,Mend] NoAI
UNIT

#define IgnoreGilliam [0x18,0x0]
SouthEastPegs:
UNIT BadGuy FalcoKnight Ephidel Level(4,Enemy,1) [16,19] 0 0 1 C_14_15 [SilverLance] IgnoreGilliam
UNIT BadGuy FalcoKnight Ephidel Level(3,Enemy,1) [16,19] 0 0 1 C_13_16 [Spear] IgnoreGilliam
UNIT

SouthWestGuys:
UNIT BadGuy Warrior Ephidel Level(4,Enemy,1) [5,19] 0 0 1 C_9_17 [SilverAxe] IgnoreGilliam
UNIT BadGuy Warrior Ephidel Level(3,Enemy,1) [5,19] 0 0 1 C_10_15 [Tomahawk] IgnoreGilliam
UNIT

SecretShopContents:
SHLI BraveSword BraveLance BraveAxe BraveBow Sleep Berserk Elixir

ALIGN 4

BeginningScene:
LOAD1 1 PlayerUnits
ENUN
CursorFlash(Eirika)
Text(Woodland, OpeningText)
LOAD1 1 BaseEnemies
ENUN
// We need to do this due to a bug in ColorzCore macro parsing that doesn't respect
// [bracketed,coordinates] as a single parameter.
#define hlcoords [11,5]
CursorFlash(hlcoords)
#undef hlcoords
Text(Woodland, OpeningBandits)
ScrollText(ExplainHidden)
ScrollText(ExplainGrade)
ScrollText(ExplainMutuallyExclusive)
CAMERA CastleCoords
CursorFlash(CastleCoords)
STAL 30
CursorFlash(CastleCoords)
ScrollText(ExplainGoToCastle)
ScrollText(ExplainSeize)
SVAL s3 (-15000)
GIVETOSLOT3
SVAL s3 29
ScrollText(DisplayNumTest)
NoFade
ENDA

LoadFlier:
LOAD1 1 Flier
ENUN
CAMERA Vanessa
CursorFlash(Vanessa)
Text(0x0905)
EVBIT_T 0x9
LOAD1 1 Turn2BadGuys
ENUN
EVBIT_F 0x9
NoFade
ENDA

SpawnReinforcements1:
EVBIT_T 0x9
LOAD1 1 TopLeftSwords
ENUN
EVBIT_F 0x9
NoFade
ENDA

SpawnReinforcements2:
EVBIT_T 0x9
LOAD1 1 SouthWestGuys
ENUN
LOAD1 1 SouthEastPegs
ENUN
EVBIT_F 0x9
NoFade
ENDA

EphidelAggro:
SVAL s1 0
CHAI Ephidel
NoFade
ENDA

MageHouse: {
  CHECK_TURNS
  SVAL s1 5
  BLT LabelDest(MageHouse,TooLate) s1 sC
  CHECK_ACTIVEID Eirika
  BEQ LabelDest(MageHouse,NotEirikaEarly) sC s0
  Text(HouseBG, EarlyMageEirika)
  GOTO LabelDest(MageHouse,LoadBoltingMage)

  NotEirikaEarly:
  SetLabel(MageHouse)
  Text(HouseBG, EarlyMageGeneric)

  LoadBoltingMage:
  SetLabel(MageHouse)
  LOAD1 1 BoltingMage
  ENUN
  GOTO LabelDest(MageHouse,Done)

  TooLate:
  SetLabel(MageHouse)
  CHECK_ACTIVEID Eirika
  BEQ LabelDest(MageHouse,NotEirikaLate) sC s0
  Text(HouseBG, LateMageEirika)
  GOTO LabelDest(MageHouse,NonBoltingMage)

  NotEirikaLate:
  SetLabel(MageHouse)
  Text(HouseBG, LateMageGeneric)

  LoadNonBoltingMage:
  SetLabel(MageHouse)
  LOAD1 1 NonBoltingMage
  ENUN
  CursorFlash(LArachel)
  Text(SorryLostBolting)

  Done:
  SetLabel(MageHouse)
  NoFade
  ENDA
}

CheckEarlyWarning: {
  CHECK_INAREA Eirika [5,16] [1,1]
  BEQ LabelDest(CheckEarlyWarning,NotThere) sC s0
  CALL SecretEnding
  GOTO LabelDest(CheckEarlyWarning,Done)

  NotThere:
  SetLabel(CheckEarlyWarning)
  CHECK_TURNS
  SVAL s1 4
  BLT LabelDest(CheckEarlyWarning,Done) s1 sC
  ENUF EarlyWarningId

  Done:
  SetLabel(CheckEarlyWarning)
  NoFade
  ENDA
}

SpawnEnemies: {
  CHECK_INAREA Eirika CastleCoords [1,1]
  BEQ LabelDest(SpawnEnemies,Failed) sC s0
  CALL SecretEnding
  GOTO LabelDest(SpawnEnemies,Done)

  Failed:
  SetLabel(SpawnEnemies)
  EVBIT_T 0x9
  LOAD1 1 EphidelGroup
  ENUN
  CAMERA Ephidel
  CursorFlash(Ephidel)
  Text(EphidelSpawnText)
  LOAD1 1 RightCavs
  ENUN
  LOAD1 1 TopLeftWyverns
  ENUN
  LOAD1 1 MiddleInfantry
  ENUN
  CHECK_EVENTID MageHouseId
  BNE LabelDest(SpawnEnemies,LArachelEarly) sC s0
  CAMERA MageHouseCoords
  CursorFlash(MageHouseCoords)
  Text(HouseBG, ShowLArachel)

  LArachelEarly:
  SetLabel(SpawnEnemies)
  LOAD1 1 GateGuard
  ENUN
  CursorFlash(Gilliam)
  Text(GilliamSpawn)
  EVBIT_F 0x9
  CAMERA Eirika
  CursorFlash(Eirika)
  Text(WhatToDo)
  CAMERA SouthFort
  LOAD1 1 EphraimReinforcements
  CursorFlash(Ephraim)
  Text(ReinforcementsArrive)
  ScrollText(RealMapStarts)
  CAMERA Ephidel
  CursorFlash(Ephidel)
  ScrollText(RealMapStarts2)
#define Highlight(coords) "CAMERA c; CursorFlash(c)"
#define c [17,14]
  Highlight(c)
#undef c
#define c [15,9]
  Highlight(c)
#undef c
#define c [4,5]
  Highlight(c)
#undef c
#define c [4,7]
  Highlight(c)
#undef c
  ScrollText(RealMapStartsSkills)
  ScrollText(RealMapStarts3)

  Done:
  SetLabel(SpawnEnemies)
  NoFade
  ENDA
}

SecretEnding: {
  Text(SecretEndingDialogue)
  ScrollText(SecretEnding1)
  ScrollText(SecretEnding2)
  ScrollText(SecretEnding3)
  ScrollText(SecretEnding4)
  ScrollText(SecretEnding5)
  CALL EndingScene
  ENDA
}

EndingScene: {
  GOTO_TITLE
  ENDA
}

DontSeizeBaka: {
  Text(DontSeize1)
  ScrollText(DontSeize2)
  ScrollText(DontSeize3)
  ScrollText(DontSeize4)
  ScrollText(DontSeize5)
  ScrollText(DontSeize6)
  ScrollText(DontSeize7)
  ScrollText(DontSeize8)
  GameOver
  NoFade
  ENDA
}

GiveMemberCard: {
  Text(GiveMemberCardText)
  SVAL s3 MemberCard
  GIVEITEMTO -1
  NoFade
  ENDA
}

TalkToGilliam: {
  Text(TalkToGilliamText)
  NoFade
  ENDA
}

MatthewEirikaChat: {
  Text(EirikaMatthew)
  NoFade
  ENDA
}

MatthewSethChat: {
  Text(SethMatthew)
  NoFade
  ENDA
}

MatthewSecretShopChat: {
  Text(MatthewSecretShop)
  NoFade
  ENDA
}

DidNotVisit: {
  SVAL s1 0
  CALL ComputeScore
  NoFade
  ENDA
}

LateVisit: {
  CHECK_EVENTID 2
  BNE LabelDest(LateVisit,EphidelDead) sC s0
  SVAL s1 10
  // TODO: alt scoring if ephidel is alive
  GOTO LabelDest(LateVisit,Done)

  EphidelDead:
  SetLabel(LateVisit)
  SVAL s1 5
  CALL ComputeScore

  Done:
  SetLabel(LateVisit)
  NoFade
  ENDA
}

ComputeScore: {
  CHECK_EVENTID 6
  BEQ LabelDest(ComputeScore, PostRout) sC s0
  SVAL s2 2
  SADD s1 s1 s2

  PostRout:
  SetLabel(ComputeScore)
}

ALIGN 4
