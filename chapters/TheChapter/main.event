#include EAstdlib.event

// Thanks to Colorz and Contro for figuring out this trick!
#define SetLabel(parentLabel) "LABEL (currentOffset - (parentLabel))"
#define LabelDest(parentLabel, destLabel) "((destLabel) - (parentLabel))"

#define ScrollText(id) "SVAL sB (-1); TUTORIALTEXTBOXSTART; TEXTSHOW (id); TEXTEND;"
#define CursorFlash(c, time) "CURSOR_FLASHING c; STAL (time); REMOVE_CURSORS;"
#define CursorFlash(c) "FlashCursor(c, 30)"

PUSH
// prologue events
EventPointerTable(0x7,ChEvents)
// prologue map
EventPointerTable(0x4,ChMap)

// prologue worldmap events
//ORG 0x8B0890+117
//BYTE 0
POP

#define CastleCoords [5,16]

#define EarlyWarningId 10
#define MageHouseId 11

#define StartBadGuy 0x8E
#define BadGuy 0x80

ALIGN 4
ChMap:
#incbin map.dmp

ALIGN 4
ChEvents:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN AltMiscEventsA AltMiscEventsB AltMiscEventsC
POIN Tutorial
POIN TrapData TrapData
POIN PlayerUnits PlayerUnits
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0,LoadFlier,2)
TurnEventPlayer(0,SpawnEnemies,4)
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
House(MageHouseId, MageHouse, 15, 12)
END_MAIN

MiscBasedEvents:
AREA EarlyWarningId CheckEarlyWarning [5,16] [5,17]
CauseGameOverIfLordDies
END_MAIN

TrapData:
END_MAIN

PlayerUnits:
UNIT Eirika EirikaMasterLord Eirika Level(1,Ally,0) [9,0] 0 0 1 EirMoves [Rapier,Vulnerary] NoAI
UNIT Seth Paladin Eirika Level(1,Ally,0) [10,0] 0 0 1 SethMoves [SilverLance,SteelSword] NoAI
UNIT

BaseEnemies:
UNIT StartBadGuy Brigand 0 Level(1,Enemy,0) [13,3] 0 0 1 BanditMoves1 [IronAxe,Vulnerary] NoAI
UNIT StartBadGuy Brigand 0 Level(1,Enemy,0) [13,3] 0 0 1 BanditMoves2 [IronAxe,Vulnerary] NoAI
UNIT

Flier:
UNIT Vanessa FalcoKnight Eirika Level(1,Ally,0) [0,7] 0 0 1 FlierMoves [SlimLance,HeavySpear] NoAI
UNIT BadGuy Sniper 0 Level(1,Enemy,0) [0,11] 0 0 1 ArcherMoves [Longbow] AttackInRangeAI
UNIT

BadGuysWave1:
UNIT

BoltingMage:
UNIT LArachel MageKnight Eirika Level(1,Ally,0) [15,12] 0 0 1 MageMoves [Bolting,Vulnerary] NoAI
UNIT

BeginningScene:
LOAD1 1 PlayerUnits
ENUN
CursorFlash(Eirika)
Text(Woodland, 0x0903)
LOAD1 1 BaseEnemies
ENUN
// We need to do this due to a bug in ColorzCore macro parsing that doesn't respect
// [bracketed,coordinates] as a single parameter.
#define hlcoords [11,5]
CursorFlash(hlcoords)
#undef hlcoords
Text(Woodland, 0x0904)
ScrollText(0x00D6)
ScrollText(0x00D7)
ScrollText(0x00D8)
CAMERA CastleCoords
CursorFlash(CastleCoords)
STAL 30
CursorFlash(CastleCoords)
ScrollText(0x00D9)
NoFade
ENDA

LoadFlier:
// "Hey, sorry I'm late!"
LOAD1 1 Flier
ENUN
ENDA

MageHouse: {
  CHECK_TURNS
  SVAL s1 5
  BLT LabelDest(MageHouse,TooLate) s1 sC
  LOAD1 1 BoltingMage
  ENUN
  GOTO LabelDest(MageHouse,Done)

  TooLate:
  SetLabel(MageHouse)
  // "oops too late, if you'd visited earlier she would have been here"

  Done:
  SetLabel(MageHouse)
  NoFade
  ENDA
}

CheckEarlyWarning: {
  CHECK_INAREA Eirika [5,16] [1,1]
  BEQ LabelDest(CheckEarlyWarning,NotThere) sC s0
  CALL SecretEnding
  GOTO Done

  NotThere:
  SetLabel(CheckEarlyWarning)
  CHECK_TURNS
  SVAL s1 4
  BLT LabelDest(CheckEarlyWarning,Done) s1 sC
  ENUF EarlyWarningId

  Done:
  SetLabel(CheckEarlyWarning)
  NoFade
  ENDA
}

SpawnEnemies: {
  CHECK_INAREA Eirika CastleCoords [1,1]
  BEQ LabelDest(SpawnEnemies,Failed) sC s0
  CALL SecretEnding
  GOTO LabelDest(SpawnEnemies,Done)

  Failed:
  SetLabel(SpawnEnemies)
  POPUP 0x00D5 0

  Done:
  SetLabel(Done)
  NoFade
  ENDA
}

SecretEnding: {
  POPUP 0x00D4 0
  CALL EndingScene
  NoFade
  ENDA
}

EndingScene: {
  NoFade
  ENDA
}

EirMoves:
REDA [8,2]
SethMoves:
REDA [6,5]
FlierMoves:
REDA [4,4]
MageMoves:
REDA [14,12]
ArcherMoves:
REDA [2,11]
BanditMoves1:
REDA [11,5]
BanditMoves2:
REDA [10,6]

ALIGN 4
