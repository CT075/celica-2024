#include EAstdlib.event
#include ParseDefinitions.event

// Thanks to Colorz and Contro for figuring out this trick!
#define SetLabel(parentLabel) "LABEL (currentOffset - (parentLabel))"
#define LabelDest(parentLabel, destLabel) "((destLabel) - (parentLabel))"

#define ScrollText(id) "SVAL sB (-1); TUTORIALTEXTBOXSTART; TEXTSHOW (id); TEXTEND;"
#define CursorFlash(c, time) "CURSOR_FLASHING c; STAL (time); REMOVE_CURSORS;"
#define CursorFlash(c) "FlashCursor(c, 30)"

PUSH
// prologue events
EventPointerTable(0x7,ChEvents)
// prologue map
EventPointerTable(0x4,ChMap)

// (initial) goal window
ORG 0x8B091A
SHORT 0x01A2

ORG 0x8B0916
BYTE -1
POP

#define CastleCoords [5,16]

#define EarlyWarningId 10
#define MageHouseId 11
#define MemberCardId 12
#define GilliamTalkId 13

#define VisitedSecretShopId 131

#define StartBadGuy 0x8E
#define BadGuy 0x80
#define TheSniper 0x8F
#define Ephidel Lyon_Ch17

#include REDAHelpers.event

ALIGN 4
ChMap:
#incbin map.dmp

ALIGN 4
ChEvents:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN AltMiscEventsA AltMiscEventsB AltMiscEventsC
POIN Tutorial
POIN TrapData TrapData
POIN PlayerUnits PlayerUnits
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0,LoadFlier,2)
TurnEventPlayer(0,SpawnEnemies,4)
END_MAIN

CharacterBasedEvents:
CharacterEvent(GilliamTalkId, TalkToGilliam, Eirika, Gilliam)
END_MAIN

LocationBasedEvents:
House(MageHouseId, MageHouse, 15, 12)
//Seize(0x3, DontSeizeBaka, CastleCoords)
Seize(0x3, DontSeizeBaka, 5, 16)
SecretShop(SecretShopContents,0,7)
END_MAIN

MiscBasedEvents:
AREA EarlyWarningId CheckEarlyWarning [5,16] [5,17]
AREA MemberCardId GiveMemberCard [0,19] [0,19]
CauseGameOverIfLordDies
END_MAIN

TrapData:
END_MAIN

#define PlayerUnit(char, cls, lvl) "UNIT char cls Eirika Level(lvl,Ally,0)"

PlayerUnits:
PlayerUnit(Eirika,EirikaMasterLord,1) [9,0] 0 0 1 C_8_2 [Rapier,Elixir] NoAI
PlayerUnit(Seth,Paladin,10) [10,0] 0 0 1 C_6_5 [SilverLance,SteelSword] NoAI
UNIT

BaseEnemies:
UNIT StartBadGuy Brigand 0 Level(18,Enemy,1) [13,3] 0 0 1 C_11_5 [IronAxe,Vulnerary] NoAI
UNIT StartBadGuy Brigand 0 Level(15,Enemy,1) [13,3] 0 0 1 C_10_6 [IronAxe,Vulnerary] NoAI
UNIT

Flier:
PlayerUnit(Vanessa,FalcoKnight,4) [0,7] 0 0 1 C_4_4 [SteelLance,SteelSword,Vulnerary] NoAI
UNIT TheSniper Sniper 0 Level(5,Enemy,0) [0,11] 0 0 1 C_2_11 [Longbow] AttackInRangeAI
UNIT

Turn2BadGuys:
UNIT BadGuy Warrior 0 Level(3,Enemy,1) [17,3] 0 0 0 0 [SilverAxe] NoAI
UNIT BadGuy Fighter 0 Level(16,Enemy,1) [17,3] 0 0 1 C_16_2 [IronAxe] NoAI
UNIT BadGuy Fighter 0 Level(18,Enemy,1) [17,3] 0 0 1 C_17_4 [SteelAxe] NoAI
UNIT

BoltingMage:
PlayerUnit(LArachel,MageKnight_F,1) [15,12] 0 0 1 C_14_12 [Elfire,Bolting,Vulnerary] NoAI
UNIT

NonBoltingMage:
PlayerUnit(LArachel,MageKnight_F,1) [15,12] 0 0 1 C_14_12 [Elfire,Physic,Vulnerary] NoAI
UNIT

GateGuard:
UNIT Gilliam General Gilliam Level(10,NPC,0) CastleCoords 0 0 0 0 [SilverLance,Elixir] NeverMoveAI
UNIT

RightCavs:
UNIT BadGuy GreatKnight 0 Level(4,Enemy,1) [19,15] 0 0 1 C_17_14 [SilverAxe,SteelLance] ChaseOnceApproachedAI
UNIT BadGuy Cavalier 0 Level(16,Enemy,1) [19,16] 0 0 1 C_18_13 [IronLance] ChaseOnceApproachedAI
UNIT BadGuy Cavalier 0 Level(18,Enemy,1) [19,15] 0 0 1 C_18_15 [SteelSword] ChaseOnceApproachedAI
UNIT

TopLeftWyverns:
UNIT BadGuy WyvernLord 0 Level(4,Enemy,1) [0,3] 0 0 1 C_4_5 [] NoAI
UNIT BadGuy WyvernRider 0 Level(19,Enemy,1) [0,3] 0 0 1 C_3_4 [] NoAI
UNIT BadGuy WyvernRider 0 Level(17,Enemy,1) [0,4] 0 0 1 C_3_6 [] NoAI
UNIT

EphidelGroup:
UNIT Ephidel Necromancer 0 Level(20,Enemy,0) [0,19] 0 0 1 C_17_3 [Naglfar] AttackInRangeAI
UNIT BadGuy Sage Ephidel Level(9,Enemy,1) [0,19] 0 0 1 C_18_4 [Fimbulvetr] AttackInRangeAI
UNIT BadGuy Sage Ephidel Level(7,Enemy,1) [1,19] 0 0 1 C_16_2 [Elfire] AttackInRangeAI
UNIT

SecretShopContents:
SHLI Audhulma Vidofnir Garm Nidhogg Excalibur Ivaldi Gleipnir

ALIGN 4

BeginningScene:
LOAD1 1 PlayerUnits
ENUN
CursorFlash(Eirika)
Text(Woodland, OpeningText)
LOAD1 1 BaseEnemies
ENUN
// We need to do this due to a bug in ColorzCore macro parsing that doesn't respect
// [bracketed,coordinates] as a single parameter.
#define hlcoords [11,5]
CursorFlash(hlcoords)
#undef hlcoords
Text(Woodland, OpeningBandits)
ScrollText(ExplainHidden)
ScrollText(ExplainGrade)
ScrollText(ExplainMutuallyExclusive)
CAMERA CastleCoords
CursorFlash(CastleCoords)
STAL 30
CursorFlash(CastleCoords)
ScrollText(ExplainGoToCastle)
ScrollText(ExplainSeize)
NoFade
ENDA

LoadFlier:
LOAD1 1 Flier
ENUN
CAMERA Vanessa
CursorFlash(Vanessa)
Text(0x0905)
EVBIT_T 0x9
LOAD1 1 Turn2BadGuys
ENUN
EVBIT_F 0x9
NoFade
ENDA

MageHouse: {
  CHECK_TURNS
  SVAL s1 5
  BLT LabelDest(MageHouse,TooLate) s1 sC
  CHECK_ACTIVEID Eirika
  BEQ LabelDest(MageHouse,NotEirikaEarly) sC s0
  Text(HouseBG, EarlyMageEirika)
  GOTO LabelDest(MageHouse,LoadBoltingMage)

  NotEirikaEarly:
  SetLabel(MageHouse)
  Text(HouseBG, EarlyMageGeneric)

  LoadBoltingMage:
  SetLabel(MageHouse)
  LOAD1 1 BoltingMage
  ENUN
  GOTO LabelDest(MageHouse,Done)

  TooLate:
  SetLabel(MageHouse)
  CHECK_ACTIVEID Eirika
  BEQ LabelDest(MageHouse,NotEirikaLate) sC s0
  Text(HouseBG, LateMageEirika)
  GOTO LabelDest(MageHouse,NonBoltingMage)

  NotEirikaLate:
  SetLabel(MageHouse)
  Text(HouseBG, LateMageGeneric)

  LoadNonBoltingMage:
  SetLabel(MageHouse)
  LOAD1 1 NonBoltingMage
  ENUN
  CursorFlash(LArachel)
  Text(SorryLostBolting)

  Done:
  SetLabel(MageHouse)
  NoFade
  ENDA
}

CheckEarlyWarning: {
  CHECK_INAREA Eirika [5,16] [1,1]
  BEQ LabelDest(CheckEarlyWarning,NotThere) sC s0
  CALL SecretEnding
  GOTO LabelDest(CheckEarlyWarning,Done)

  NotThere:
  SetLabel(CheckEarlyWarning)
  CHECK_TURNS
  SVAL s1 4
  BLT LabelDest(CheckEarlyWarning,Done) s1 sC
  ENUF EarlyWarningId

  Done:
  SetLabel(CheckEarlyWarning)
  NoFade
  ENDA
}

SpawnEnemies: {
  CHECK_INAREA Eirika CastleCoords [1,1]
  BEQ LabelDest(SpawnEnemies,Failed) sC s0
  CALL SecretEnding
  GOTO LabelDest(SpawnEnemies,Done)

  Failed:
  SetLabel(SpawnEnemies)
  EVBIT_T 0x9
  LOAD1 1 EphidelGroup
  ENUN
  CursorFlash(Ephidel)
  Text(EphidelSpawnText)
  LOAD1 1 GateGuard
  ENUN
  CursorFlash(Gilliam)
  Text(GilliamSpawn)
  EVBIT_F 0x9

  Done:
  SetLabel(Done)
  NoFade
  ENDA
}

SecretEnding: {
  Text(SecretEndingDialogue)
  ScrollText(SecretEnding1)
  ScrollText(SecretEnding2)
  ScrollText(SecretEnding3)
  ScrollText(SecretEnding4)
  ScrollText(SecretEnding5)
  CALL EndingScene
  ENDA
}

EndingScene: {
  GOTO_TITLE
  ENDA
}

DontSeizeBaka: {
  Text(DontSeize1)
  ScrollText(DontSeize2)
  ScrollText(DontSeize3)
  ScrollText(DontSeize4)
  ScrollText(DontSeize5)
  ScrollText(DontSeize6)
  ScrollText(DontSeize7)
  ScrollText(DontSeize8)
  GameOver
  NoFade
  ENDA
}

GiveMemberCard: {
  Text(GiveMemberCardText)
  SVAL s3 MemberCard
  GIVEITEMTO -1
  NoFade
  ENDA
}

TalkToGilliam: {
  Text(TalkToGilliamText)
  NoFade
  ENDA
}

ALIGN 4
